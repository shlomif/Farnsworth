stma= stmt [";" stma];

stmt= expr
	| "var" NAME
	| "var" NAME "=" expr
	| NAME ":=" expr
	| expr ":->" expr
	| NAME "=!=" NAME
	| expr "|||" NAME
	| if
	| while
	;

ifstartcond= "if" "(" expr ")"
           ;

ifstmts="{" stma "}"
       ;

if= ifstartcond ifstmts [ "else" ifstmts ]
  ;

while= "while" "(" expr ")" "{" stma "}"
	 ;

array= [ expr ] [ "," array ];

constraint= expr
		  | "..."
		  ;

argelement= NAME [ "=" expr ] [ "isa" constraint ]
		  | NAME "byref" [ "isa" constraint ];

arglistfilled= argelement [ "," arglistfilled ] ;
 
arglist= [arglistfilled]; 

assignexpr=	expr "=" expr
		   ;

assignexpr2= expr "+=" expr
		   | expr "-=" expr
		   | expr "/=" expr
		   | expr "*=" expr
		   | expr "%=" expr
		   | expr "**=" expr
		   | expr "^=" expr
		   ;

assigncomb= assignexpr
		  |	assignexpr2
		  ;

lambda= "{`" arglist "`" stma "}"
	  ;

multexpr= expr "*" expr
	    | singleval singleval [ expr ]
	    | expr " " expr
	    ;

logic= expr "&&" expr
	 | expr "||" expr
	 | expr "^^" expr
	 | "!" singleval
	 ;

compare= expr "<" expr
	   | expr ">" expr
	   | expr "<=" expr
       | expr ">=" expr
       | expr "==" expr
       | expr "<=>" expr
       | expr "!=" expr
       ;

crement= "++" singleval
	   | "--" singleval
	   | singleval "++"
	   | singleval "--"
	   ;

number= NUMBER
      | HEXNUMBER
	  ;	   

singleval= value
         | parens
         ;

value=	number
     |	DATE
	 |	STRING
	 |	singleval "@" array "$"
	 |	NAME
	 |   "[" array "]"
	 |   lambda
	 ;

parens=	"(" expr ")"
	  ;
	  
expr= exprnouminus
	| "-" expr
	;

exprnouminus= singleval
            | multexpr
            | expr "**" expr
            | expr "^" expr
            | expr "+" expr
            | expr "-" expr
            | expr "/" expr
            | expr "per" expr
            | expr "%" expr
            | expr "conforms" expr
            | expr "?" expr ":" expr
            | logic
            | compare
            | crement
            | NAME "::-"  expr
            | NAME ":-" expr
            | NAME "{" arglist "}" ":=" expr
            | NAME "{" arglist "}" ":=" "{" stma "}"
            | expr "=>" expr
            | expr "->" expr
            | assigncomb
            ;
