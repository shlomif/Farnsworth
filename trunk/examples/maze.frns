#!./farnsworth.pl
/*also written by b_jonas*/
/* farbir.farn -- maze generator
Copyright (C) Zsban Ambrus 2001--2009 */
F{n,s} := { var k=0; while (k < n) { s[k]; k=k+1; }; };
var W = 35;
var H = 19;
var a = [];
a0{} := { var l = []; F[3+2*W, {`k` push[l,1] }]; push[a,l] };
a0[];
F[1+2*H, {`k`
	var l = [1]; F[1+2*W, {`k` push[l, 0] }]; push[l,1]; push[a,l];
}];
a0[];
F[H, {`u`
	F[W, {`v`
		a@2+2*u$@2+2*v$ = 1;
	}];
}];
var Y = [];
F[W*H, {`k` push[Y, -1]; }];
C{v} := {
	var w = Y@v$;
	if (w < 0) {
		v;
	} else {
		var z = C[w];
		Y@v$ = z;
		z;
	};
};
U{u, v}:= {
	var c = C[u]; 
	var d = C[v];
	if (c != d) { 
		Y@c$ = d;
	};
};
while (C[0] != C[W*H-1]) {
	var b = 1 < 2*random[];
	var x = int[H*random[]];
	var y = int[W*random[]];
	if (b ? y < W - 1 : x < H - 1) {
		var u = (x+(b?0:1))*W+(y+(b?1:0));
		var v = x*W+y;
		if (C[u] != C[v] || 5*random[] < 1) {
			U[u, v];
			a@2+(b?0:1)+2*x$@2+(b?1:0)+2*y$ = 1;
		};
	};
};
var os = chr[10];
print{s} := { os = os + s; };
F[1+2*H, {`r0`
	var r = 1 + r0;
	F[1+2*W, {`c0`
		var c = 1 + c0;
		var o = a@r$@c$;
		var v = a@r-1$@c$ && a@r+1$@c$;
		var h = a@r$@c-1$ && a@r$@c+1$;
		print[
			2 == r && 2 == c ? "@" :
			2*H == r && 1+2*W == c ? ">" :
			o ? " " :
			v ? (h ? " " : "-") :
			(h ? "|" : "+")
		];
	}];
	print[chr[10]];
}];
error[os]

